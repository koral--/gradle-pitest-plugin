buildscript {
    ext.kotlin_version = '1.9.24'
    ext.junit5Version = '5.10.3'

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.10.0.0"
    }
}

group = "pitest.test"

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'pl.droidsonroids.pitest'
apply plugin: 'de.mannodermaus.android-junit5'

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
}

pitest {
    junit5PluginVersion = "1.2.1"
    avoidCallsTo = ["kotlin.jvm.internal"]
    mutators = ["STRONGER"]
    targetClasses = ['pitest.test.*']  //by default "${project.group}.*"
    targetTests = ['pitest.test.*Test']
//    pitestVersion = "1.4.0"   //current defined for Gradle plugin PIT version should be used
    threads = Runtime.getRuntime().availableProcessors()
    outputFormats = ['XML', 'HTML']
}

android {
    namespace 'pl.drodsonroids.pitest'
    compileSdkVersion 34
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
    }
    lintOptions {
        //ignore missing lint database
        abortOnError false
    }
}

kotlin {
    jvmToolchain(17)
}
