buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        //Local/current version of the plugin should be put on a classpath earlier to override that plugin version
//        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.3-SNAPSHOT'
    }
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }

    version = '1.0'
    group = 'pitest.sample.multimodule'
}

configure(project(":shared")) {
    apply plugin: 'com.android.library'

    android {
        compileSdkVersion 32
        defaultConfig {
            minSdkVersion 10
            targetSdkVersion 32
        }

    }

    dependencies {
        api 'org.slf4j:slf4j-api:2.0.5'
        testImplementation 'junit:junit:4.13.2'
    }
}

configure(project(':itest')) {
    buildscript {
        repositories {
            google()
            mavenCentral()
        }
    }
    apply plugin: 'com.android.application'
    apply plugin: 'pl.droidsonroids.pitest'

    android {
        compileSdkVersion 32
        defaultConfig {
            minSdkVersion 10
            targetSdkVersion 32
        }
    }
    dependencies {
        implementation project(':shared')
        testImplementation 'junit:junit:4.13.2'
    }

    //Additional configuration to resolve :shared project JAR as mutable code path for PIT
    configurations {
        mutableCodeBase { transitive false }
        dependencies {
            mutableCodeBase.extendsFrom(project(':shared').configurations.implementation)
        }
    }
    pitest {
        timestampedReports = false
        mainSourceSets = [project.android.sourceSets.main, project(':shared').android.sourceSets.main]
    }
}
