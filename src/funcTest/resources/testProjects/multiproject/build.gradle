buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        //Local/current version of the plugin should be put on a classpath earlier to override that plugin version
//        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.3-SNAPSHOT'
    }
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'junit:junit:4.12'
    }

    version = '1.0'
    group = 'pitest.sample.multimodule'
}

configure(project(":shared")) {
    dependencies {
        implementation 'org.slf4j:slf4j-api:1.7.25'
        implementation 'org.slf4j:slf4j-nop:1.7.25'
    }
}

configure(project(':itest')) {
    apply plugin: "info.solidsoft.pitest"
    dependencies {
        implementation project(':shared')
    }

    //Additional configuration to resolve :shared project JAR as mutable code path for PIT
    configurations {
        mutableCodeBase { transitive = false }
        dependencies {
            mutableCodeBase.extendsFrom(project(':shared').configurations.implementation)
        }
    }
    pitest {
        timestampedReports = false
        mainSourceSets = [project.sourceSets.main, project(':shared').sourceSets.main]
        //Generates deprecation warning in Gradle 5.6+
        //Asked for recommendation: https://discuss.gradle.org/t/accessing-other-module-dependency-files-without-mutable-project-state-warnings/35048
//        additionalMutableCodePaths = configurations.mutableCodeBase.files
        additionalMutableCodePaths = project(':shared').jar.outputs.files.getFiles()  //Workaround
    }
}
