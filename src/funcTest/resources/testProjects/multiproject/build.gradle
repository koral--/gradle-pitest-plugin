buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        //Local/current version of the plugin should be put on a classpath earlier to override that plugin version
//        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.3-SNAPSHOT'
    }
}

subprojects {
    apply plugin: 'pl.droidsonroids.pitest'

    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    version = '1.0'
    group = 'pitest.sample.multimodule'
}

configure(project(":shared")) {
    apply plugin: 'com.android.library'

    android {
        compileSdkVersion 28
        defaultConfig {
            minSdkVersion 10
            targetSdkVersion 28
        }

    }

    dependencies {
        api 'org.slf4j:slf4j-api:1.7.25'
        testImplementation 'junit:junit:4.12'
    }
}

configure(project(':itest')) {
    apply plugin: 'com.android.application'

    android {
        compileSdkVersion 28
        defaultConfig {
            minSdkVersion 10
            targetSdkVersion 28
        }
    }
    dependencies {
        implementation project(':shared')
        testImplementation 'junit:junit:4.12'
    }

    //Additional configuration to resolve :shared project JAR as mutable code path for PIT
    configurations { mutableCodeBase { transitive false } }
    dependencies { mutableCodeBase project(path: ':shared', configuration: 'default') }
    pitest {
        timestampedReports = false
        mainSourceSets = [project.android.sourceSets.main, project(':shared').android.sourceSets.main]
        additionalMutableCodePaths = [configurations.mutableCodeBase.singleFile]
//        additionalMutableCodePaths = project(':shared').jar.outputs.files.getFiles()  //Should also work
    }
}
